@pack(size: int, n1: int, n2: int, n3: int, n4: int, n5: int): ptr<int> {
  one.1: int = const 1;
  i.1: int = const 0;
  array.1: ptr<int> = alloc size;
  loc.1: ptr<int> = ptradd array.1 i.1;
  store loc.1 n1;
  i.2: int = add i.1 one.1;
  loc.2: ptr<int> = ptradd array.1 i.2;
  store loc.2 n2;
  i.3: int = add i.2 one.1;
  loc.3: ptr<int> = ptradd array.1 i.3;
  store loc.3 n3;
  i.4: int = add i.3 one.1;
  loc.4: ptr<int> = ptradd array.1 i.4;
  store loc.4 n4;
  i.5: int = add i.4 one.1;
  loc.5: ptr<int> = ptradd array.1 i.5;
  store loc.5 n5;
  ret array.1;
}
@print_array(array: ptr<int>, size: int) {
  i.1: int = const 0;
  one.1: int = const 1;
.loop:
  cond.1: bool = lt i.1 size;
  br cond.1 .body .done;
.body:
  loc.1: ptr<int> = ptradd array i.1;
  val.1: int = load loc.1;
  print val.1;
.loop_end:
  i.1: int = add i.1 one.1;
  jmp .loop;
.done:
  ret;
}
@main(size: int, n1: int, n2: int, n3: int, n4: int, n5: int) {
  array.1: ptr<int> = call @pack size n1 n2 n3 n4 n5;
  zero.1: int = const 0;
  one.1: int = const 1;
  i.2: int = id one.1;
.loop_outer:
  cond_i.1: bool = lt i.2 size;
  br cond_i.1 .body_outer .done_outer;
.body_outer:
  loc_i.1: ptr<int> = ptradd array.1 i.2;
  val_i.1: int = load loc_i.1;
  j.2: int = sub i.2 one.1;
.loop_inner:
  cond_j.1: bool = ge j.2 zero.1;
  br cond_j.1 .check_value .insert;
.check_value:
  loc_j.1: ptr<int> = ptradd array.1 j.2;
  val_j.1: int = load loc_j.1;
  cond_val.1: bool = gt val_j.1 val_i.1;
  br cond_val.1 .shift .insert;
.shift:
  j_plus_1.1: int = add j.2 one.1;
  loc_j_plus_1.1: ptr<int> = ptradd array.1 j_plus_1.1;
  store loc_j_plus_1.1 val_j.1;
  j.2: int = sub j.2 one.1;
  jmp .loop_inner;
.insert:
  j_plus_1.2: int = add j.2 one.1;
  loc_insert.1: ptr<int> = ptradd array.1 j_plus_1.2;
  store loc_insert.1 val_i.1;
.loop_outer_end:
  i.2: int = add i.2 one.1;
  jmp .loop_outer;
.done_outer:
  call @print_array array.1 size;
  free array.1;
}
